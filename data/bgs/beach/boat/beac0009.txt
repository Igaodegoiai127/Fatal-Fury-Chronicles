#Boat for sound beach stage.

name	 		beac0009
health	 		5
type	 		obstacle
scroll			0.17
#speedf   		0.17
setlayer 		-8
globalmap		1
offscreenkill	1000000
height			100
nomove			1
falldie			1
nodieblink		2
no_adjust_base	1
antigravity		100

animationscript	 data/scripts/z_blank.c
onspawnscript	 data/scripts/spaw0005.c
takedamagescript data/bgs/beach/boat/scripts/damage.c
onkillscript	 data/bgs/beach/boat/scripts/kill.c
ondeathscript	 data/bgs/beach/boat/scripts/death.c

anim idle 

	loop	1
	delay	14
	offset	55 150
	bbox	15 60 70 100
	@script
		void vSelf = getlocalvar("self");
		int  iHeight	= 0.0 + getentityproperty(vSelf, "a");		
		int	 iBase		= 0.0 + getentityproperty(vSelf, "base");
		int	 iY			= getentityproperty(vSelf, "tossv");
		int	 iDiff		= iHeight - iBase;	
			
		if(iY>0)
		{					
			if(iDiff > 4)
			{
				changeentityproperty(vSelf, "velocity", 0, 0, -0.05);				
			}
		}
		else if(iY<=0)
		{			
			if(iDiff <= 0) 
			{					
				changeentityproperty(vSelf, "velocity", 0, 0, 0.05);	
			}
		}				
	@end_script	
		frame data/bgs/beach/boat/0001		
		frame data/bgs/beach/boat/0002	
		frame data/bgs/beach/boat/0003	

anim death	
	
	#I'm the king of the world! glub glub glub....

	loop    1
	delay	3
	offset	56 150
	bbox	0
	@script
		void vSelf = getlocalvar("self");
		int  iBase = 0.0 + getentityproperty(vSelf, "base");		
		int	 iHeight = getentityproperty(vSelf, "a");

		changeentityproperty(vSelf, "base", -180);
		changeentityproperty(vSelf, "velocity", 0,0,-0.1);		

		if (iHeight <= iBase)
		{
			killentity(vSelf);
		}		
	@end_script
		frame data/bgs/beach/boat/0001
	offset	54 150
		frame data/bgs/beach/boat/0001
	
